plugins {
    id 'com.android.library'
    id 'maven-publish'
}

android {
    compileSdkVersion 34

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 34
        versionCode 14
        versionName "1.1.11"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    sourceSets {
        main {
            jniLibs.srcDirs = ['libs']
        }
    }

    //引入依赖路径
    repositories {
        flatDir {
            dirs 'libs'
        }
    }

    publishing {
        singleVariant('release') {
            withSourcesJar()
            withJavadocJar()
        }
    }

}

def jarName = "AILinkSDKOtaLibraryAndroid.jar"
task makeJar(type: Jar) {
    delete 'build/libs/' + jarName
    from zipTree("build/intermediates/compile_library_classes_jar/release/classes.jar")
    from fileTree(dir: 'src/main/java') //需要保留的资源文件,源码等
    archiveName = jarName
    destinationDir = file('build/libs')
}
makeJar.dependsOn(build)


dependencies {
//    implementation fileTree(dir: "libs", include: ['*.jar'])
    implementation 'com.github.elinkthings:AILinkSDKRepositoryAndroid:1.15.01'

    //杰理OTA库
    implementation(name: 'jl_bt_ota_V1.9.2-release', ext: 'aar')
    //杰理rcsp基础库
    implementation(name: 'jl_rcsp_V0.5.1-release', ext: 'aar')
    //图像转换相关
    implementation(name: 'BmpConvert_V1.2.0-release', ext: 'aar')
    //杰理健康相关
    implementation(name: 'JL_Watch_V1.9.1-release', ext: 'aar')
    implementation(name: 'rtkOtaLib-release', ext: 'aar')

}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                groupId = 'com.github.elinkthings'
                artifactId = 'AILinkSDKOtaLibraryAndroid'
                version = "1.1.11"

                // 发布生成的 AAR 文件
//                artifact("$buildDir/outputs/aar/${project.name}-release.aar")
                from components.release

                // 添加必要的 POM 元数据
                pom {
                    name = 'AILink SDK OTA Library'
                    description = 'Bluetooth OTA library for Android'
                    url = 'https://github.com/elinkthings/AILinkSDKOtaLibraryAndroid'

                    licenses {
                        license {
                            name = 'The Apache License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }

                    developers {
                        developer {
                            id = 'elinkthings'
                            name = 'ElinkThings Team'
                            email = 'app@elinkthings.com'
                        }
                    }
                }



                // 添加 AAR 依赖到 POM
                pom.withXml {
                    def dependenciesNode = asNode().dependencies
                    if (dependenciesNode.isEmpty()) {
                        dependenciesNode = asNode().appendNode('dependencies')
                    } else {
                        dependenciesNode = dependenciesNode.get(0)
                    }

                    // 确保只添加必要的依赖，避免重复
                    // 添加 AAR 依赖
                    def addDependency = { group, artifact, version, scope = 'runtime' ->
                        def found = dependenciesNode.'*'.find {
                            it.groupId.text() == group && it.artifactId.text() == artifact
                        }
                        if (!found) {
                            def dependencyNode = dependenciesNode.appendNode('dependency')
                            dependencyNode.appendNode('groupId', group)
                            dependencyNode.appendNode('artifactId', artifact)
                            dependencyNode.appendNode('version', version)
                            dependencyNode.appendNode('scope', scope)
                        }
                    }

                    // 添加您的依赖项
                    addDependency("com.jieli", "jl_bt_ota", "1.9.2")
                    addDependency("com.jieli", "jl_rcsp", "0.5.1")
                    addDependency("com.jieli", "BmpConvert", "1.2.0")
                    addDependency("com.jieli", "JL_Watch", "1.9.1")
                    addDependency("com.rtk", "rtkOtaLib", "1.0.0")
                }
            }
        }
    }
}